use.std::utils

use.miden::kernels::tx::epilogue
use.miden::kernels::tx::memory
use.miden::kernels::tx::note
use.miden::kernels::tx::prologue

# TRACES
# =================================================================================================

# Trace emitted to signal that an execution of the transaction prologue has started.
const.PROLOGUE_START=131072
# Trace emitted to signal that an execution of the transaction prologue has ended.
const.PROLOGUE_END=131073

# Trace emitted to signal that the notes processing has started.
const.NOTES_PROCESSING_START=131074
# Trace emitted to signal that the notes processing has ended.
const.NOTES_PROCESSING_END=131075

# Trace emitted to signal that the note consuming has started.
const.NOTE_EXECUTION_START=131076
# Trace emitted to signal that the note consuming has ended.
const.NOTE_EXECUTION_END=131077

# Trace emitted to signal that the transaction script processing has started.
const.TX_SCRIPT_PROCESSING_START=131078
# Trace emitted to signal that the transaction script processing has ended.
const.TX_SCRIPT_PROCESSING_END=131079

# Trace emitted to signal that an execution of the transaction epilogue has started.
const.EPILOGUE_START=131080
# Trace emitted to signal that an execution of the transaction epilogue has ended.
const.EPILOGUE_END=131081

# MAIN
# =================================================================================================

#! Transaction kernel program.
#!
#! This is the entry point of the transaction kernel, the program will perform the following
#! operations:
#!
#! 1. Run the prologue to prepare the transaction's root context.
#! 2. Run all the notes' scripts.
#! 3. Run the transaction script.
#! 4. Run the epilogue to compute and validate the final state.
#!
#! See `prologue::prepare_transaction` for additional details on the VM's initial state, including the
#! advice provider.
#!
#! Stack:   [BLOCK_HASH, account_id, INITIAL_ACCOUNT_HASH, NULLIFIER_COMMITMENT]
#! Output:  [CREATED_NOTES_COMMITMENT, FINAL_ACCOUNT_HASH]
#!
#! Where:
#! - BLOCK_HASH, reference block for the transaction execution.
#! - account_id, the account that the transaction is being executed against.
#! - INITIAL_ACCOUNT_HASH, account state prior to the transaction, ZERO for new accounts.
#! - NULLIFIER_COMMITMENT, sequential hash of all input notes' nullifiers.
#! - CREATED_NOTES_COMMITMENT, commitment to the notes created by the transaction.
#! - FINAL_ACCOUNT_HASH, account's hash after execution the transaction.
proc.main.1
    # Prologue
    # ---------------------------------------------------------------------------------------------
    push.0 drop                         # TODO: remove line, see miden-vm/#1122
    trace.PROLOGUE_START

    exec.prologue::prepare_transaction
    # => []

    push.0 drop                         # TODO: remove line, see miden-vm/#1122
    trace.PROLOGUE_END

    # Note Processing
    # ---------------------------------------------------------------------------------------------

    push.0 drop                         # TODO: remove line, see miden-vm/#1122
    trace.NOTES_PROCESSING_START

    exec.memory::get_total_num_consumed_notes
    # => [num_consumed_notes]

    # compute the memory location after all input notes, i.e. the exit condition
    dup exec.memory::get_consumed_note_ptr loc_store.0
    # => [num_consumed_notes]

    eq.0 not
    # => [should_loop]

    while.true
        push.0 drop                     # TODO: remove line, see miden-vm/#1122
        trace.NOTE_EXECUTION_START
        # => []

        exec.note::prepare_note
        # => [NOTE_SCRIPT_HASH, NOTE_ARGS]

        # run note's script
        dyncall
        # => [X, X, X, X]

        # Clear the stack, the note can leave up to 4 words on the stack due to the dyncall
        dropw dropw dropw dropw
        # => []

        exec.note::increment_current_consumed_note_ptr
        # => [current_consumed_note_ptr]

        # loop condition, exit when the memory ptr is after all input notes
        loc_load.0 neq
        # => [should_loop]

        push.0 drop                     # TODO: remove line, see miden-vm/#1122
        trace.NOTE_EXECUTION_END
    end

    exec.note::note_processing_teardown
    # => []

    push.0 drop                         # TODO: remove line, see miden-vm/#1122
    trace.NOTES_PROCESSING_END

    # Transaction Script Processing
    # ---------------------------------------------------------------------------------------------

    push.0 drop                         # TODO: remove line, see miden-vm/#1122
    trace.TX_SCRIPT_PROCESSING_START

    # execute the transaction script
    exec.memory::get_tx_script_root
    # => [TX_SCRIPT_ROOT]

    exec.utils::is_empty_word not
    # => [has_tx_script, TX_SCRIPT_ROOT]

    if.true
        # execute the transaction script
        dyncall
        # => [OUTPUT_3, OUTPUT_2, OUTPUT_1, OUTPUT_0]

        # clean up transaction script outputs
        dropw dropw dropw dropw
        # => []
    else
        # drop the empty transaction script root word
        dropw
        # => []
    end

    push.0 drop                         # TODO: remove line, see miden-vm/#1122
    trace.TX_SCRIPT_PROCESSING_END

    # Epilogue
    # ---------------------------------------------------------------------------------------------

    push.0 drop                         # TODO: remove line, see miden-vm/#1122
    trace.EPILOGUE_START

    # execute the transaction epilogue
    exec.epilogue::finalize_transaction
    # => [CREATED_NOTES_COMMITMENT, FINAL_ACCOUNT_HASH]

    push.0 drop                         # TODO: remove line, see miden-vm/#1122
    trace.EPILOGUE_END
    # => [CREATED_NOTES_COMMITMENT, FINAL_ACCOUNT_HASH]
end

begin
    exec.main
end
