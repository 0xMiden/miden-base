#! Returns the block number of the last known block at the time of transaction execution.
#!
#! Inputs: []
#! Outputs: [num]
#!
#! num is the last known block number.
export.get_block_number
    push.0
    # => [0]

    syscall.get_block_number
    # => [num]
end

#! Returns the block hash of the last known block at the time of transaction execution.
#!
#! Inputs: []
#! Outputs: [H]
#!
#! H is the last known block hash.
export.get_block_hash
    padw
    # => [0, 0, 0, 0]

    syscall.get_block_hash
    # => [H]
end

#! Returns the input notes hash. This is computed as a sequential hash of (nullifier, script_root)
#! tuples over all input notes.
#!
#! Inputs: []
#! Outputs: [COM]
#!
#! COM is the input notes hash.
export.get_input_notes_hash
    padw
    # => [0, 0, 0, 0]

    syscall.get_input_notes_hash
    # => [COM]
end

#! Returns the output notes hash. This is computed as a sequential hash of (note_hash, note_metadata)
#! tuples over all output notes.
#!
#! Inputs: [0, 0, 0, 0]
#! Outputs: [COM]
#!
#! COM is the output notes hash.
export.get_output_notes_hash
    padw
    # => [0, 0, 0, 0]

    syscall.get_output_notes_hash
    # => [COM]
end

#! Creates a new note and returns a pointer to the memory address at which the note is stored.
#!
#! Inputs: [tag, aux, note_type, RECIPIENT]
#! Outputs: [ptr]
#!
#! tag is the tag to be included in the note.
#! aux is an auxiliary field that can be used by the user
#! note_type is the storage type of the note
#! RECIPIENT is the recipient of the note.
#! ptr is the pointer to the memory address at which the note is stored.
export.create_note
    syscall.create_note
    # => [ptr, ZERO, 0, 0]

    # clear the padding from the kernel response
    movdn.6 dropw drop drop
    # => [ptr]
end

#! Creates a new note and returns a pointer to the memory address at which the note is stored.
#!
#! Inputs: [ptr, ASSET]
#! Outputs: [ptr]
#!
#! ptr is the pointer to the memory address at which the note is stored.
#! ASSET can be a fungible or non-fungible asset.
export.add_asset_to_note
    syscall.add_asset_to_note
    # => [ptr, ZERO]

    # clear the padding from the kernel response
    movdn.4 dropw swap drop
    # => [ptr]
end
