#! Returns the block number of the last known block at the time of transaction execution.
#!
#! Inputs: []
#! Outputs: [num]
#!
#! num is the last known block number.
export.get_block_number
    push.0
    # => [0]

    syscall.get_block_number
    # => [num]
end

#! Returns the block hash of the reference block.
#!
#! Stack: [EMPTY_WORD]
#! Output: [BLOCK_HASH]
#!
#! Where:
#! - BLOCK_HASH, reference block for the transaction execution.
export.get_block_hash
    padw syscall.get_block_hash
    # => [BLOCK_HASH]
end

#! Returns the input notes commitment hash.
#!
#! See `transaction::api::get_input_notes_commitment` for details.
#!
#! Stack: []
#! Outputs: [INPUT_NOTES_COMMITMENT]
#!
#! Where:
#! - INPUT_NOTES_COMMITMENT is the input notes commitment hash.
export.get_input_notes_commitment
    padw syscall.get_input_notes_commitment
    # => [INPUT_NOTES_COMMITMENT]
end

#! Returns the output notes hash. This is computed as a sequential hash of (note_id, note_metadata)
#! tuples over all output notes.
#!
#! Inputs: [0, 0, 0, 0]
#! Outputs: [COM]
#!
#! COM is the output notes hash.
export.get_output_notes_hash
    padw
    # => [0, 0, 0, 0]

    syscall.get_output_notes_hash
    # => [COM]
end

#! Creates a new note and returns the index of the note.
#!
#! Inputs:  [tag, aux, note_type, execution_hint, RECIPIENT, ...]
#! Outputs: [note_idx, ...]
#!
#! tag is the tag to be included in the note.
#! aux is the auxiliary metadata to be included in the note.
#! note_type is the storage type of the note
#! execution_hint is the note's execution hint
#! RECIPIENT is the recipient of the note.
#! note_idx is the index of the crated note.
export.create_note
    # pad the stack before the syscall to prevent accidental modification of the deeper stack 
    # elements 
    padw padw swapdw
    # => [tag, aux, note_type, execution_hint, RECIPIENT, PAD(8)]
    syscall.create_note
    # => [note_idx, PAD(15)]

    # remove excess PADs from the stack 
    swapdw dropw dropw movdn.7 dropw drop drop drop
    # => [note_idx]
end

#! Adds the ASSET to the note specified by the index.
#!
#! Inputs:  [ASSET, note_idx, ...]
#! Outputs: [ASSET, note_idx, ...]
#!
#! note_idx is the index of the note to which the asset is added.
#! ASSET can be a fungible or non-fungible asset.
export.add_asset_to_note
    # pad the stack before the syscall to prevent accidental modification of the deeper stack 
    # elements 
    push.0.0.0 padw padw swapdw movup.7 swapw movup.4
    # => [note_idx, ASSET, PAD(11)]

    syscall.add_asset_to_note 
    # => [note_idx, ASSET, PAD(11)]

    # remove excess PADs from the stack 
    swapdw dropw dropw swapw movdn.7 drop drop drop movdn.4
    # => [ASSET, note_idx]
end

#! Returns the RECIPIENT for a specified SERIAL_NUM, SCRIPT_HASH, and inputs hash
#!
#! Inputs: [SERIAL_NUM, SCRIPT_HASH, INPUT_HASH]
#! Outputs: [RECIPIENT]
#!
export.build_recipient_hash
  padw hmerge
  # => [SERIAL_NUM_HASH, SCRIPT_HASH, INPUT_HASH]

  swapw hmerge
  # => [MERGE_SCRIPT, INPUT_HASH]

  swapw hmerge
  # [RECIPIENT]
end
