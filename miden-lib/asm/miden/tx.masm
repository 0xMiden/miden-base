#! Returns the block number of the last known block at the time of transaction execution.
#!
#! Inputs: []
#! Outputs: [num]
#!
#! num is the last known block number.
export.get_block_number
    push.0
    # => [0]

    syscall.get_block_number
    # => [num]
end

#! Returns the block hash of the reference block.
#!
#! Stack: [ZERO]
#! Output: [BLOCK_HASH]
#!
#! Where:
#! - BLOCK_HASH, reference block for the transaction execution.
export.get_block_hash
    padw syscall.get_block_hash
    # => [BLOCK_HASH]
end

#! Returns the input notes hash. This is computed as a sequential hash of (nullifier, script_root)
#! tuples over all input notes.
#!
#! Inputs: []
#! Outputs: [COM]
#!
#! COM is the input notes hash.
export.get_input_notes_hash
    padw
    # => [0, 0, 0, 0]

    syscall.get_input_notes_hash
    # => [COM]
end

#! Returns the output notes hash. This is computed as a sequential hash of (note_hash, note_metadata)
#! tuples over all output notes.
#!
#! Inputs: [0, 0, 0, 0]
#! Outputs: [COM]
#!
#! COM is the output notes hash.
export.get_output_notes_hash
    padw
    # => [0, 0, 0, 0]

    syscall.get_output_notes_hash
    # => [COM]
end

#! Creates a new note and returns a pointer to the memory address at which the note is stored.
#!
#! Inputs: [tag, note_type, RECIPIENT]
#! Outputs: [note_ptr]
#!
#! tag is the tag to be included in the note.
#! note_type is the storage type of the note
#! RECIPIENT is the recipient of the note.
#! note_ptr is the pointer to the memory address at which the note is stored.
export.create_note
    syscall.create_note
    # => [note_ptr, ZERO, 0]

    # clear the padding from the kernel response
    movdn.4 dropw swap drop
    # => [note_ptr]
end

#! Adds an ASSET to the specified note and returns a pointer to the memory address
#! at which the note is stored.
#!
#! Inputs: [note_ptr, ASSET]
#! Outputs: [note_ptr]
#!
#! note_ptr is the pointer to the memory address at which the note is stored.
#! ASSET can be a fungible or non-fungible asset.
export.add_asset_to_note
    syscall.add_asset_to_note
    # => [note_ptr, ZERO]

    # clear the padding from the kernel response
    movdn.4 dropw
    # => [note_ptr]
end

#! Returns the RECIPIENT for a specified SERIAL_NUM, SCRIPT_HASH, and input
#!
#! Inputs: [SERIAL_NUM, SCRIPT_HASH, input]
#! Outputs: [RECIPIENT]
#!
#! Only allows a single input currently   
export.build_recipient_hash
  padw hmerge
  # => [SERIAL_NUM_HASH, SCRIPT_HASH, input] 

# merge SERIAL_NUM_HASH and SCRIPT_HASH
  swapw hmerge
  # => [SERIAL_SCRIPT_HASH, input] 

  # compute the INPUT_HASH. Note: only one input is allowed
  swapw swap.3 padw hmerge
  # => [INPUT_HASH, SERIAL_SCRIPT_HASH]

  hmerge
  # [RECIPIENT]
end