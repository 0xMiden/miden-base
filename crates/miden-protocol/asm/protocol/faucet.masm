use miden::protocol::asset
use miden::protocol::active_account
use miden::protocol::kernel_proc_offsets

#! Creates a fungible asset for the faucet the transaction is being executed against.
#!
#! Inputs:  [amount]
#! Outputs: [ASSET]
#!
#! Where:
#! - amount is the amount of the asset to create.
#! - ASSET is the created fungible asset.
#!
#! Panics if:
#! - the active account is not a fungible faucet.
#!
#! Invocation: exec
pub proc create_fungible_asset
    # fetch the id of the faucet the transaction is being executed against.
    exec.active_account::get_id
    # => [id_prefix, id_suffix, amount]

    # build the fungible asset
    exec.asset::build_fungible_asset
    # => [ASSET]
end

#! Creates a non-fungible asset for the faucet the transaction is being executed against.
#!
#! Inputs:  [DATA_HASH]
#! Outputs: [ASSET]
#!
#! Where:
#! - DATA_HASH is the data hash of the non-fungible asset to create.
#! - ASSET is the created non-fungible asset.
#!
#! Panics if:
#! - the active account is not a non-fungible faucet.
#!
#! Invocation: exec
pub proc create_non_fungible_asset
    # get the id of the faucet the transaction is being executed against
    exec.active_account::get_id swap drop
    # => [faucet_id_prefix, DATA_HASH]

    # build the non-fungible asset
    exec.asset::build_non_fungible_asset
    # => [ASSET]
end

#! Mint an asset from the faucet the transaction is being executed against.
#!
#! Inputs:  [ASSET]
#! Outputs: [ASSET]
#!
#! Where:
#! - ASSET is the asset that was minted.
#!
#! Panics if:
#! - the transaction is not being executed against a faucet.
#! - the asset being minted is not associated with the faucet the transaction is being executed
#!   against.
#! - the asset is not well formed.
#! - for fungible faucets if the total issuance after minting is greater than the maximum amount
#!   allowed.
#! - for non-fungible faucets if the non-fungible asset being minted already exists.
#!
#! Invocation: exec
pub proc mint
    exec.kernel_proc_offsets::faucet_mint_asset_offset
    # => [offset, ASSET]

    # pad the stack
    push.0.0.0 movdn.7 movdn.7 movdn.7 padw padw swapdw
    # => [offset, ASSET, pad(11)]

    syscall.exec_kernel_proc
    # => [ASSET, pad(12)]

    # clean the stack
    swapdw dropw dropw swapw dropw
    # => [ASSET]
end

#! Burn an asset from the faucet the transaction is being executed against.
#!
#! Inputs:  [ASSET]
#! Outputs: [ASSET]
#!
#! Where:
#! - ASSET is the asset that was burned.
#!
#! Panics if:
#! - the transaction is not being executed against a faucet.
#! - the asset being burned is not associated with the faucet the transaction is being executed
#!   against.
#! - the asset is not well formed.
#! - for fungible faucets if the amount being burned is greater than the total input to the
#!   transaction.
#! - for non-fungible faucets if the non-fungible asset being burned does not exist or was not
#!   provided as input to the transaction via a note or the accounts vault.
#!
#! Invocation: exec
pub proc burn
    exec.kernel_proc_offsets::faucet_burn_asset_offset
    # => [offset, ASSET]

    # pad the stack
    push.0.0.0 movdn.7 movdn.7 movdn.7 padw padw swapdw
    # => [offset, ASSET, pad(11)]

    syscall.exec_kernel_proc
    # => [ASSET, pad(12)]

    # clean the stack
    swapdw dropw dropw swapw dropw
    # => [ASSET]
end
