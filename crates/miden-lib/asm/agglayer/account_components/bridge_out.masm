use.miden::note
use.miden::output_note
use.miden::active_note
use.std::math::u64

const.BURN_NOTE_ROOT=[6407337173854817345,5626358912819151014,703918618794810515,17401169215223723177]

const.EXECUTION_HINT_ALWAYS=1
const.PUBLIC_NOTE=1
const.AUX=0
const.DEFAULT_FAUCET_TAG=0

# Computes the tag of the BURN note such that it is picked
# up by the network transaction builder
# same logic as in Rust in NoteTag::from_network_account_id()
#! Inputs: [ASSET]
#! Output: [network_faucet_tag]
proc compute_network_faucet_tag
  movdn.3 drop drop drop
  # => [account_id_prefix]

  u32split
  # => [a_hi, a_lo]

  push.34
  # => [b, a_hi, a_lo]

  exec.u64::shr
  # => [c_hi, c_lo]

  drop
  # => [network_faucet_tag]
end


# Computes the SERIAL_NUM of the outputted BURN note
#   hash(B2AGG_SERIAL_NUM, ASSET) 
#! Inputs: [ASSET]
#! Output: [SERIAL_NUM]
proc compute_burn_note_serial_num
  exec.active_note::get_serial_number
  # => [B2AGG_SERIAL_NUM, ASSET]

  hmerge
  # => [SERIAL_NUM]
end

#! Inputs: [ASSET]
#! Outputs: []
proc create_burn_note.8
    loc_storew_be.0 dupw
    # => [ASSET]

    exec.compute_network_faucet_tag
    # => [network_faucet_tag, ASSET]

    loc_store.5
    # => [ASSET]

    exec.compute_burn_note_serial_num
    # => [SERIAL_NUM]

    push.BURN_NOTE_ROOT swapw
    # => [SERIAL_NUM, SCRIPT_ROOT]

    push.0 push.0
    # => [inputs_ptr, num_inputs, SERIAL_NUM, SCRIPT_ROOT]

    exec.note::build_recipient
    # => [RECIPIENT]

    push.EXECUTION_HINT_ALWAYS
    push.PUBLIC_NOTE
    push.AUX
    loc_load.5
    # => [tag, aux, note_type, execution_hint, RECIPIENT]

    call.output_note::create
    # => [note_idx, 0,0,0]

    padw loc_loadw_be.0
    # => [ASSET, note_idx, 0,0,0]

    exec.output_note::add_asset
    # => [REQUESTED_ASSET, note_idx, 0,0,0]

    dropw dropw
    # => []
end

#! Stubbed out
#! Inputs: [ASSET, INPUTS[1], INPUTS[0]]
#! Outputs: [LEAF[1], LEAF[0]]
proc mmr_frontier_keccak_add
  dropw dropw dropw
  push.1.1.1.1
  push.2.2.2.2 
  # => [LEAF[1], LEAF[0]]
end

# Stubbed out
#! Inputs: []
#! Output: [ROOT[1], ROOT[0]]
pub proc mmr_frontier_keccak_get_root
  push.3.3.3.3
  push.4.4.4.4
end

#! Inputs: [ASSET, INPUTS[1], INPUTS[0]]
#! Outputs: []
pub proc bridge_out.4
    loc_storew_be.0
    # => [ASSET, INPUTS[1], INPUTS[0]]

    exec.mmr_frontier_keccak_add
    # => [LEAF[1], LEAF[0]]

    exec.mmr_frontier_keccak_get_root
    # => [ROOT[1], ROOT[0], LEAF[1], LEAF[0]]

    dropw dropw dropw dropw
    # => []

    loc_loadw_be.0
    # => [ASSET]

    exec.create_burn_note
    # => []
end
