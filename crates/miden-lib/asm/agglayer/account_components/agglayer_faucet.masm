use.agglayer::bridge_in
use.miden::active_account
use.miden::contracts::faucets
use.miden::tx

# CONSTANTS
# =================================================================================================

# The slot in this component's storage layout where the bridge account ID is stored.
const.BRIDGE_ID_IDX=word("miden::agglayer::faucet")

# ERRORS
# =================================================================================================
const.ERR_INVALID_PROOF="invalid proof"

#! Validates a CLAIM note's rollup exit root Merkle Proof against the agglayer bridge via FPI.
#!
#! This procedure performs a foreign procedure invocation to the agglayer bridge to validate
#! that the CLAIM note's rollup exit root Merkle Proof is valid.
#!
#! Inputs:  []
#! Outputs: []
#!
#! Panics if:
#! - the bridge account ID is not properly configured in storage.
#! - the foreign procedure invocation fails.
#! - the claim proof validation fails.
#!
#! Invocation: exec
proc validate_claim_via_fpi
    # get check_claim_proof root
    procref.bridge_in::check_claim_proof
    # => [MAST_ROOT]

    push.BRIDGE_ID_IDX[0..2]
    # => [bridge_id_idx, MAST_ROOT]

    # get bridge account id
    exec.active_account::get_item
    # => [bridge_account_id_prefix, bridge_account_id_suffix, 0, 0, MAST_ROOT]

    movup.2 drop movup.2 drop
    # => [bridge_account_id_prefix, bridge_account_id_suffix, MAST_ROOT]

    # call check_claim_proof proc on bridge
    exec.tx::execute_foreign_procedure
    # => [validation_result]

    assert.err=ERR_INVALID_PROOF drop
    # => []
end

#! Distributes freshly minted fungible assets to the provided recipient by creating a note.
#!
#! This procedure validates the rollup exit root Merkle Proof via FPI against the agglayer bridge,
#! and if validation passes, mints the asset and creates an output note for the recipient.
#!
#! Inputs:  [amount, tag, aux, note_type, execution_hint, RECIPIENT, pad(8)]
#! Outputs: [pad(16)]
#!
#! Where:
#! - amount is the amount to be minted and sent.
#! - tag is the tag to be included in the note.
#! - aux is the auxiliary data to be included in the note.
#! - note_type is the type of the note that holds the asset.
#! - execution_hint is the execution hint of the note that holds the asset.
#! - RECIPIENT is the recipient of the asset, i.e.,
#!   hash(hash(hash(serial_num, [0; 4]), script_root), input_commitment).
#!
#! Panics if:
#! - the rollup exit root Merkle Proof validation via FPI fails.
#! - any of the validations in faucets::distribute fail.
#!
#! Invocation: call
export.claim
    exec.faucets::distribute
    # => [pad(16)]

    # validate CLAIM note proof (stubbed out)
    exec.validate_claim_via_fpi
    # => [pad(16)]
end

#! Burns the fungible asset from the active note.
#!
#! This procedure retrieves the asset from the active note and burns it. The note must contain
#! exactly one asset, which must be a fungible asset issued by this faucet.
#!
#! Inputs:  [pad(16)]
#! Outputs: [pad(16)]
#!
#! Panics if:
#! - the procedure is not called from a note context (active_note::get_assets will fail).
#! - the note does not contain exactly one asset.
#! - the transaction is executed against an account which is not a fungible asset faucet.
#! - the transaction is executed against a faucet which is not the origin of the specified asset.
#! - the amount about to be burned is greater than the outstanding supply of the asset.
#!
#! Invocation: call
export.faucets::burn