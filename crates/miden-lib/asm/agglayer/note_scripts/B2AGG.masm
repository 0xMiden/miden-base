use.agglayer::bridge_out -> agglayer
use.miden::account_id
use.miden::active_account
use.miden::active_note

# CONSTANTS
# =================================================================================================

const.B2AGG_NOTE_INPUTS_COUNT=6

# ERRORS
# =================================================================================================
const.ERR_B2AGG_WRONG_NUMBER_OF_ASSETS="B2AGG script requires exactly 1 note asset"

const.ERR_B2AGG_WRONG_NUMBER_OF_INPUTS="B2AGG script expects exactly 6 note inputs"

#! Bridge-to-AggLayer (B2AGG) note script: bridges assets from Miden to an AggLayer-connected chain.
#!
#! This note can be consumed in two ways:
#! - If the consuming account is the sender (reclaim): adds all assets from the note back to the account.
#! - Otherwise: bridges the note's assets to the specified destination network and address via AggLayer.
#!
#! Inputs:  []
#! Outputs: []
#!
#! Note inputs are assumed to be as follows:
#! - destination_network: u32 value representing the target chain ID
#! - destination_address: split into 5 u32 values representing a 20-byte Ethereum address:
#!   - destination_address_0: bytes 0-3
#!   - destination_address_1: bytes 4-7
#!   - destination_address_2: bytes 8-11
#!   - destination_address_3: bytes 12-15
#!   - destination_address_4: bytes 16-19
#!
#! Stack layout after loading inputs:
#! - INPUTS[1]: [destination_network, destination_address_4, destination_address_3, destination_address_2]
#! - INPUTS[0]: [destination_address_1, destination_address_0, 0, 0]
#!
#! Panics if:
#! - The note does not contain exactly 6 inputs.
#! - The note does not contain exactly 1 asset.
#!
begin
    dropw
    # => [pad(16)]

    # Check if reclaim
    exec.active_account::get_id
    # => [account_id_prefix, account_id_suffix, pad(16)]

    exec.active_note::get_sender
    # => [sender_id_prefix, sender_id_suffix, account_id_prefix, account_id_suffix, pad(16)]

    exec.account_id::is_equal
    # => [reclaim, pad(16)]

    # B2AGG note is being reclaimed; adding note assets to account
    if.true
        exec.active_note::add_assets_to_account
        # => [pad(16)]
    else
        # Store note inputs -> mem[8..14]
        push.8 exec.active_note::get_inputs
        # => [num_inputs, dest_ptr, pad(16)]

        push.B2AGG_NOTE_INPUTS_COUNT assert_eq.err=ERR_B2AGG_WRONG_NUMBER_OF_INPUTS drop
        # => [pad(16)]

        # Store number of note assets -> mem[0]
        push.0 exec.active_note::get_assets
        # => [num_assets, ptr, pad(16)]

        # Must be exactly 1 asset
        push.1 assert_eq.err=ERR_B2AGG_WRONG_NUMBER_OF_ASSETS drop
        # => [pad(16)]

        # load the 6 B2AGG note input felts as two words
        mem_loadw_be.8 swapw.2 mem_loadw_be.12 swapw
        # => [EMPTY_WORD, INPUTS[1], INPUTS[0], pad(4)]

        # Load ASSET onto the stack
        mem_loadw_be.0
        # => [ASSET, INPUTS[1], INPUTS[0], pad(4)]

        call.agglayer::bridge_out
        # => [pad(16)]
    end
    # => [pad(16)]
end
