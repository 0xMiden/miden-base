use.agglayer::bridge_out -> agglayer
use.miden::account_id
use.miden::active_account
use.miden::active_note

const.ERR_B2AGG_WRONG_NUMBER_OF_ASSETS="B2AGG script requires exactly 1 note asset"

#! Inputs:  [destination_network(u32), destination_address_0(u32), destination_address_1(u32),
#!           destination_address_2(u32), destination_address_3(u32), destination_address_4(u32)]
#! Outputs: []
#!
#! The destination_address is split into 5 u32 values representing a 20-byte Ethereum address:
#! - destination_address_0: bytes 0-3
#! - destination_address_1: bytes 4-7
#! - destination_address_2: bytes 8-11
#! - destination_address_3: bytes 12-15
#! - destination_address_4: bytes 16-19
#!
begin

    # Check if reclaim
    exec.active_account::get_id
    # => [account_id_prefix, account_id_suffix]

    exec.active_note::get_sender
    # => [sender_id_prefix, sender_id_suffix, account_id_prefix, account_id_suffix]

    exec.account_id::is_equal
    # => [reclaim]

    if.true
        exec.active_note::add_assets_to_account
        # => []
    else
        # Store number of note assets -> mem[0]
        push.0 exec.active_note::get_assets
        # => [num_assets, ptr]

        # Must be exactly 1 asset
        assert.err=ERR_B2AGG_WRONG_NUMBER_OF_ASSETS
        # => [ptr]

        # Load ASSET onto the stack
        mem_loadw_be.0
        # => [ASSET, destination_network, DESTINATION_ADDRESS]

        call.agglayer::bridge_out
        # => []
    end
    # => []

    dropw dropw
end
