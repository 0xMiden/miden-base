use.miden::account
use.miden::tx

# CONSTANTS
# =================================================================================================

# Event to place the falcon signature of a provided message and public key on the advice stack.
const.FALCON_SIG_TO_STACK=131087

# The slot in this component's storage layout where the public key is stored.
const.PUBLIC_KEY_SLOT=0

# The slot where the number of trigger procedures is stored.
const.NUM_TRIGGER_PROCS_SLOT=1

# The slot where the map of trigger procedure roots is stored.
const.TRIGGER_PROCS_MAP_SLOT=2

#! Authenticate a transaction using the Falcon signature scheme only if certain procedures were called
#!
#! This authentication procedure checks if any of the trigger procedures were called during the
#! transaction. If none were called, authentication is skipped. If at least one was called,
#! the standard RpoFalcon512 signature verification is performed.
#!
#! Inputs:  [pad(16)]
#! Outputs: [pad(16)]
export.auth__tx_rpo_falcon512_procedure_acl
    # Get the number of trigger procedures
    push.NUM_TRIGGER_PROCS_SLOT exec.account::get_item drop drop drop
    # => [num_trigger_procs, pad(16)]

    # Check if any trigger procedure was called
    # Counter `i` starts at `num_trigger_procs` and flag `was_any_called` starts at 0
    push.0
    # => [was_any_called, i, pad(16)]

    # Loop through trigger procedures
    dup.1 neq.0
    while.true
        # => [was_any_called, i, pad(16)]

        # Get the trigger procedure root from storage
        dup.1 sub.1 push.0.0.0 push.TRIGGER_PROCS_MAP_SLOT
        # => [TRIGGER_PROCS_MAP_SLOT, [0, 0, 0, i-1], was_any_called, i, pad(16)]

        exec.account::get_map_item
        # => [TRIGGER_PROC_ROOT, was_any_called, i, pad(16)]

        exec.account::was_procedure_called
        # => [was_called, was_any_called, i, pad(16)]

        # Update was_any_called
        or
        # => [was_any_called', i, pad(16)]

        swap sub.1 swap
        # => [was_any_called', i-1, pad(16)]

        # Check if we should continue looping
        dup.1 neq.0
        # => [should_continue, was_any_called', i-1, pad(16)]
    end

    # => [was_any_called, i-1, pad(16)]

    # If any trigger procedure was called, perform signature verification
    if.true
        exec.::miden::contracts::auth::basic::auth__tx_rpo_falcon512
    end
    drop
end
