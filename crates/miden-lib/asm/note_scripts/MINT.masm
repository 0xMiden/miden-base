use.miden::active_note
use.miden::contracts::faucets::network_fungible->network_faucet
use.miden::note
use.std::sys

# CONSTANTS
# =================================================================================================

const.MINT_NOTE_NUM_INPUTS_PRIVATE=8
const.MINT_NOTE_MIN_NUM_INPUTS_PUBLIC=12

const.OUTPUT_NOTE_TYPE_PUBLIC=1
const.OUTPUT_NOTE_TYPE_PRIVATE=2

const.OUTPUT_PUBLIC_NOTE_INPUTS_ADDR=12
const.OUTPUT_PUBLIC_NOTE_INPUTS_LEN_MEM_ADDR=0

# ERRORS
# =================================================================================================
const.ERR_MINT_WRONG_NUMBER_OF_INPUTS="MINT script expects exactly 8 inputs for private or 12+ inputs for public output notes"

#! Network Faucet MINT script: mints assets by calling the network faucet's distribute function.
#! This note is intended to be executed against a network fungible faucet account.
#!
#! Requires that the account exposes:
#! - miden::contracts::faucets::network_fungible::distribute procedure.
#!
#! Inputs:  [ARGS, pad(12)]
#! Outputs: [pad(16)]
#!
#! Note inputs support two modes. Depending on the number of note inputs,
#! a private or public note is created on consumption of the MINT note:
#!
#! Private mode (8 inputs) - creates a private note:
#! - execution_hint: Execution hint for the output note
#! - aux: Auxiliary data for the output note
#! - tag: Note tag for the output note
#! - amount: The amount to mint
#! - RECIPIENT: The recipient digest (4 elements)
#!
#! Public mode (12+ inputs) - creates a public note with variable-length inputs:
#! - execution_hint: Execution hint for the output note
#! - aux: Auxiliary data for the output note
#! - tag: Note tag for the output note
#! - amount: The amount to mint
#! - SCRIPT_ROOT: Script root of the output note (4 elements)
#! - SERIAL_NUM: Serial number of the output note (4 elements)
#! - [INPUTS]: Variable-length inputs for the output note (Vec<Felt>)
#!             The number of output note inputs = num_mint_note_inputs - 12
#!
#! Panics if:
#! - account does not expose distribute procedure.
#! - the number of inputs is not exactly 8 for private or less than 12 for public output notes.
begin
    dropw
    # => [pad(16)]

    # Load note inputs into memory starting at address 0
    push.0 exec.active_note::get_inputs
    # => [total_inputs, inputs_ptr, pad(16)]

    dup
    # => [num_inputs, num_inputs, inputs_ptr, pad(16)]

    gte.MINT_NOTE_BASE_CASE_INPUTS_PUBLIC
    # => [is_public_output_note, total_inputs, inputs_ptr, pad(16)]

    if.true
        # public output note creation

        movdn.5 drop
        # => [EMPTY_WORD, total_inputs, pad(16)]

        mem_loadw_be.4
        # => [SCRIPT_ROOT, total_inputs, pad(12)]

        padw mem_loadw_be.8
        # => [SERIAL_NUM, SCRIPT_ROOT, total_inputs, pad(12)]

        movup.8 sub.MINT_NOTE_BASE_CASE_INPUTS_PUBLIC
        # => [num_inputs_output, SERIAL_NUM, SCRIPT_ROOT, pad(12)]

        push.OUTPUT_PUBLIC_NOTE_INPUTS_ADDR
        # => [inputs_ptr = 12, num_inputs_output, SERIAL_NUM, SCRIPT_ROOT, pad(12)]

        exec.note::build_recipient
        # => [RECIPIENT, pad(12)]

        padw mem_loadw_be.0
        # => [amount, tag, aux, execution_hint, RECIPIENT, pad(12)]

        push.OUTPUT_NOTE_TYPE_PUBLIC
        # => [note_type, amount, tag, aux, execution_hint, RECIPIENT, pad(12)]

        movdn.3
        # => [amount, tag, aux, note_type, execution_hint, RECIPIENT, pad(12)]

    else
        # private output note creation

        eq.MINT_NOTE_INPUTS_PRIVATE assert.err=ERR_MINT_WRONG_NUMBER_OF_INPUTS drop
        # => [inputs_ptr, pad(16)]

        drop
        # => [pad(16)]

        mem_loadw_be.4
        # => [RECIPIENT, pad(12)]

        padw mem_loadw_be.0
        # => [amount, tag, aux, execution_hint, RECIPIENT, pad(12)]

        push.OUTPUT_NOTE_TYPE_PRIVATE
        # => [note_type, amount, tag, aux, execution_hint, RECIPIENT, pad(12)]

        movdn.3
        # => [amount, tag, aux, note_type, execution_hint, RECIPIENT, pad(12)]

    end
    # => [amount, tag, aux, note_type, execution_hint, RECIPIENT, pad(12)]

    call.network_faucet::distribute
    # => [pad(21))]

    dropw drop
    # => [pad(16)]
end
