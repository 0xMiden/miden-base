use.miden::account
use.miden::auth
use.miden::tx
use.std::crypto::dsa::rpo_falcon512
use.std::crypto::hashes::rpo
use.std::word

# CONSTANTS
# =================================================================================================

# Auth Request Constants

# The event to request an authentication signature.
const.AUTH_REQUEST=131087

# The event emitted when a signature is not found for a required signer.
const.UNAUTHORIZED_EVENT=131101

# Storage Layout Constants
#
# ┌─────────────────────────────┬──────────┬──────────────┐
# │    THRESHOLD & APPROVERS    │ PUB KEYS │ EXECUTED TXS │
# │ [0, 0, threshold,           │   (map)  │    (map)     │
# │    num_approvers] (slot)   │          │              │
# ├─────────────────────────────┼──────────┼──────────────┤
# │              0              │    1     │      2       │
# └─────────────────────────────┴──────────┴──────────────┘

# The slot in this component's storage layout where both the signature threshold
# and number of approvers are stored as [0, 0, threshold, num_approvers].
const.THRESHOLD_CONFIG_SLOT=0

# The slot in this component's storage layout where the public keys map is stored.
# Map entries: [0, 0, 0, key_index] => owner_public_key
const.PUBLIC_KEYS_MAP_SLOT=1

# The slot in this component's storage layout where executed transactions are stored.
# Map entries: transaction_message => [0, 0, 0, USED_FLAG]
const.EXECUTED_TXS_SLOT=2

# Memory Constants

# Local Memory Addresses
const.NUM_OF_APPROVERS=0
const.THRESHOLD=4
const.TEMP_PK=8
const.SUCCESSFUL_VERIFICATIONS=12

# Executed Transaction Flag Constant
const.USED_FLAG=1

# ERRORS
const.TX_ALREADY_EXECUTED="failed to approve multisig transaction as it was already executed"

#! Check if transaction has already been executed and add it to executed transactions for replay protection.
#!
#! Panics if:
#! - the same transaction has already been executed
#!
#! Inputs:  [MSG]
#! Outputs: []
proc.assert_new_tx
    push.0.0.0.USED_FLAG
    # => [USED_FLAG, MSG]

    swapw
    # => [MSG, USED_FLAG]

    push.EXECUTED_TXS_SLOT
    # => [index, MSG, USED_FLAG]

    # Set the key value pair in the map to mark transaction as executed
    exec.account::set_map_item
    # => [OLD_MAP_ROOT, OLD_MAP_VALUE]

    dropw padw
    # => [EMPTY_WORD, OLD_MAP_VALUE]

    assert_eqw.err=TX_ALREADY_EXECUTED
    # => []
end

#! Verify signatures for all required signers in a loop.
#!
#! This procedure iterates through the required number of signers, fetches their public keys
#! from the account storage map, and verifies their signatures against the transaction message.
#! If any required signature is missing, it emits an UNAUTHORIZED_EVENT.
#!
#! Inputs:  [num_of_approvers, num_required_signatures, MSG]
#! Outputs: []
proc.verify_signatures.16
    dup loc_store.NUM_OF_APPROVERS
    # => [num_of_approvers, num_required_signatures, MSG]

    swap
    # => [num_required_signatures, num_of_approvers, MSG]

    loc_store.THRESHOLD
    # => [num_of_approvers, MESSAGE]

    # Initializing SUCCESSFUL_VERIFICATIONS local memory address to 0
    push.0 loc_store.SUCCESSFUL_VERIFICATIONS
    # => [num_of_approvers, MESSAGE]

    # Counter `i` starts at `num_of_approvers` and counts down to 0
    # => [i, MSG]

    # Loop through required signers and verify signatures.
    # ---------------------------------------------------------------------------------------------

    dup neq.0
    while.true
        # => [i, MSG]

        # Fetch public key from storage map.
        # -----------------------------------------------------------------------------------------

        sub.1 dup push.0.0.0 push.PUBLIC_KEYS_MAP_SLOT
        # => [owner_key_slot, [0, 0, 0, i-1], i-1, MSG]

        exec.account::get_map_item
        # => [OWNER_PUB_KEY, i-1, MSG]

        loc_storew.TEMP_PK
        # => [OWNER_PUB_KEY, i-1, MSG]

        # Check if signature exists for this signer.
        # -----------------------------------------------------------------------------------------

        movup.4 movdn.8 swapw dupw movdnw.2
        # => [MSG, OWNER_PUB_KEY, MSG, i-1]

        hmerge
        # => [SIG_KEY, MSG, i-1]

        adv.has_mapkey
        # => [SIG_KEY, MSG, i-1]

        adv_push.1
        # => [has_signature, SIG_KEY, MSG, i-1]

        # if h(msg, pk) => msg exists in AdviceMap check the signature
        if.true
            dropw dupw
            # => [MSG, MSG, i-1]

            # Verify the signature against the public key and message.
            # -----------------------------------------------------------------------------------------

            padw loc_loadw.TEMP_PK
            # => [PK, MSG, MSG, i-1]

            swapw
            # => [MSG, PK, MSG, i-1]

            # Emit the authentication request event that pushes a signature for the message to the advice stack.
            emit.AUTH_REQUEST

            swapw
            # => [PK, MSG, MSG, i-1]

            exec.rpo_falcon512::verify
            # => [MSG, i-1]

            loc_load.SUCCESSFUL_VERIFICATIONS
            add.1
            loc_store.SUCCESSFUL_VERIFICATIONS
            # => [MSG, i-1]

        else
            dropw
            # => [MSG, i-1]
        end
        # => [MSG, i-1]

        movup.4
        # => [i-1, MSG]

        dup neq.0
        # => [should_continue, i-1, MSG]
    end
    # => [i-1, MSG]

    # Check number of successful signature verifications
    # -----------------------------------------------------------------------------------------

    drop
    # => [MSG]

    loc_load.SUCCESSFUL_VERIFICATIONS
    # => [num_verifications, MSG]

    loc_load.THRESHOLD
    # => [threshold, num_verifications, MSG]

    gte not
    # => [is_unauthorized, MSG]

    # If signatures are non-existent the tx will fail here.
    if.true
        emit.UNAUTHORIZED_EVENT
        push.1 assertz.err="insufficient number of signatures"
    end
    # => [MSG]

    dropw
    # => []
end

#! Authenticate a transaction using the Falcon signature scheme with multi-signature support.
#!
#! This procedure implements multi-signature authentication by:
#! 1. Computing the transaction summary message that needs to be signed
#! 2. Verifying signatures from multiple required signers against their public keys
#! 3. Ensuring the minimum threshold of valid signatures is met
#! 4. Implementing replay protection by tracking executed transactions
#!
#! The procedure first increments the account nonce, then computes and verifies signatures for
#! the following message (in memory order):
#! [ACCOUNT_DELTA_COMMITMENT, INPUT_NOTES_COMMITMENT,
#!  OUTPUT_NOTES_COMMITMENT, [0, 0, ref_block_num, final_nonce]]
#!
#! Inputs:
#!   Operand stack: [SALT]
#!   Advice map: {
#!     h(SIG_0, MSG): SIG_0,
#!     h(SIG_1, MSG): SIG_1,
#!     h(SIG_n, MSG): SIG_n
#!   }
#! Outputs:
#!   Operand stack: []
#!
#! Where:
#! - SALT is the salt value used for transaction processing.
#! - SIG_i is the signature from the i-th signer.
#! - MSG is the transaction message being signed.
#! - h(SIG_i, MSG) is the hash of the signature and message used as the advice map key.
#!
#! Panics if:
#! - insufficient number of valid signatures (below threshold).
#! - the same transaction has already been executed (replay protection).
#!
#! Invocation: call
export.auth__tx_rpo_falcon512_multisig
    exec.account::incr_nonce drop
    # => [SALT]

    # ------ Computing transaction summary ------

    exec.auth::create_tx_summary
    # => [SALT, OUTPUT_NOTES_COMMITMENT, INPUT_NOTES_COMMITMENT, ACCOUNT_DELTA_COMMITMENT]

    exec.auth::adv_insert_hqword
    # => [SALT, OUTPUT_NOTES_COMMITMENT, INPUT_NOTES_COMMITMENT, ACCOUNT_DELTA_COMMITMENT]

    # the commitment to the tx summary is the message that is signed
    exec.auth::hash_tx_summary
    # => [TX_SUMMARY_COMMITMENT]

    dupw
    # => [TX_SUMMARY_COMMITMENT, TX_SUMMARY_COMMITMENT]

    # ------ Verifying owner signatures ------

    push.THRESHOLD_CONFIG_SLOT
    # => [index, TX_SUMMARY_COMMITMENT, TX_SUMMARY_COMMITMENT]

    exec.account::get_item
    # => [0, 0, num_of_approvers, num_required_signatures, TX_SUMMARY_COMMITMENT, TX_SUMMARY_COMMITMENT]

    drop drop
    # => [num_of_approvers, num_required_signatures, TX_SUMMARY_COMMITMENT, TX_SUMMARY_COMMITMENT]

    exec.verify_signatures
    # => [TX_SUMMARY_COMMITMENT]

    # ------ Writing executed transaction MSG to map ------

    exec.assert_new_tx
    # => [TX_SUMMARY_COMMITMENT]

end
