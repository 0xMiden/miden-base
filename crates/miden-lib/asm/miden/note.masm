use.miden::account_id
use.std::crypto::hashes::rpo
use.std::mem

#Â ERRORS
# =================================================================================================

const.ERR_PROLOGUE_NOTE_STORAGE_LEN_EXCEEDED_LIMIT="note storage length exceeded the maximum limit of 128"

# NOTE UTILITY PROCEDURES
# =================================================================================================

#! Computes the commitment to the note storage starting at the specified memory address.
#!
#! This procedure checks that the provided note storage length is within limits and then computes
#! the commitment.
#!
#! Notice that the note storage are padded with zeros in case their number is not a multiple of 8.
#!
#! If the note storage length is 0, procedure returns the empty word: [0, 0, 0, 0].
#!
#! Inputs:  [storage_ptr, storage_len]
#! Outputs: [STORAGE_COMMITMENT]
#!
#! Cycles:
#! - If number of elements divides by 8: 56 cycles + 3 * words
#! - Else: 189 cycles + 3 * words
#!
#! Panics if:
#! - storage_ptr is not word-aligned (i.e., is not a multiple of 4).
#! - storage_len is greater than 128.
#!
#! Invocation: exec
export.compute_storage_commitment
    # check that storage length is less than 128
    dup.1 push.128 u32assert2.err=ERR_PROLOGUE_NOTE_STORAGE_LEN_EXCEEDED_LIMIT
    u32lte assert.err=ERR_PROLOGUE_NOTE_STORAGE_LEN_EXCEEDED_LIMIT
    # => [storage_ptr, storage_len]

    # push 1 as the pad_inputs flag: we should pad the stack while computing the note storage
    # commitment
    push.1 movdn.2
    # => [storage_ptr, storage_len, pad_inputs_flag]

    exec.rpo::prepare_hasher_state
    exec.rpo::hash_memory_with_state
    # => [STORAGE_COMMITMENT]
end

#! Returns the max allowed number of input values per note.
#!
#! Stack: []
#! Output: [max_inputs_per_note]
#!
#! - max_inputs_per_note is the max inputs per note.
export.::miden::util::note::get_max_inputs_per_note

#! Writes the assets data stored in the advice map to the memory specified by the provided
#! destination pointer.
#!
#! Inputs:
#!   Operand stack: [ASSETS_COMMITMENT, num_assets, dest_ptr]
#!   Advice map: {
#!     ASSETS_COMMITMENT: [[ASSETS_DATA]]
#!   }
#! Outputs:
#!   Operand stack: [num_assets, dest_ptr]
export.write_assets_to_memory
    # load the asset data from the advice map to the advice stack
    adv.push_mapval
    # OS => [ASSETS_COMMITMENT, num_assets, dest_ptr]
    # AS => [[ASSETS_DATA]]

    # calculate number of assets rounded up to an even number
    dup.4 dup is_odd add
    # OS => [even_num_assets, ASSETS_COMMITMENT, num_assets, dest_ptr]
    # AS => [[ASSETS_DATA]]

    # prepare the stack for the `pipe_preimage_to_memory` procedure
    dup.6 swap
    # OS => [even_num_assets, dest_ptr, ASSETS_COMMITMENT, num_assets, dest_ptr]
    # AS => [[ASSETS_DATA]]

    # write the data from the advice stack into memory
    exec.mem::pipe_preimage_to_memory drop
    # OS => [num_assets, dest_ptr]
    # AS => []
end

#! Builds the recipient hash from note storage, script root, and serial number.
#!
#! This procedure computes the commitment of the note storage and then uses it to calculate the note
#! recipient by hashing this commitment, the provided script root, and the serial number.
#!
#! Inputs:
#!   Operand stack: [storage_ptr, storage_len, SERIAL_NUM, SCRIPT_ROOT]
#!   Advice map: {
#!      STORAGE_COMMITMENT: [STORAGE_ITEMS],
#!   }
#! Outputs:
#!   Operand stack: [RECIPIENT]
#!   Advice map: {
#!      STORAGE_COMMITMENT: [STORAGE_ITEMS],
#!      RECIPIENT: [SERIAL_SCRIPT_HASH, STORAGE_COMMITMENT],
#!      SERIAL_SCRIPT_HASH: [SERIAL_HASH, SCRIPT_ROOT],
#!      SERIAL_HASH: [SERIAL_NUM, EMPTY_WORD],
#!   }
#!
#! Where:
#! - storage_ptr is the memory address where the note storage are stored.
#! - storage_len is the storage length.
#! - SCRIPT_ROOT is the script root of the note.
#! - SERIAL_NUM is the serial number of the note.
#! - RECIPIENT is the commitment to the input note's script, storage, and the serial number.
#!
#! Locals:
#! - 0: storage_len
#!
#! Panics if:
#! - storage_ptr is not word-aligned (i.e., is not a multiple of 4).
#! - storage_len is greater than 128.
#!
#! Invocation: exec
export.build_recipient.1
    dup.1 dup.1
    # => [storage_ptr, storage_len, storage_ptr, storage_len, SERIAL_NUM, SCRIPT_ROOT]

    exec.compute_storage_commitment
    # => [STORAGE_COMMITMENT, storage_ptr, storage_len, SERIAL_NUM, SCRIPT_ROOT]

    # store storage_len into local memory
    dup.5 loc_store.0
    # => [STORAGE_COMMITMENT, storage_ptr, storage_len, SERIAL_NUM, SCRIPT_ROOT]

    locaddr.0 add.1
    locaddr.0
    # => [storage_len_start_ptr, storage_len_end_ptr, STORAGE_COMMITMENT, storage_ptr, storage_len, SERIAL_NUM, SCRIPT_ROOT]

    dup.5 dup.5 dup.5 dup.5
    # => [STORAGE_COMMITMENT, storage_len_start_ptr, storage_len_end_ptr,
    #     STORAGE_COMMITMENT, storage_ptr, storage_len, SERIAL_NUM, SCRIPT_ROOT]

    # compute the advice map key for storage_len by hashing the storage commitment
    hash
    # => [hash(STORAGE_COMMITMENT), storage_len_start_ptr, storage_len_end_ptr,
    #     STORAGE_COMMITMENT, storage_ptr, storage_len, SERIAL_NUM, SCRIPT_ROOT]

    adv.insert_mem
    dropw drop drop
    # => [STORAGE_COMMITMENT, storage_ptr, storage_len, SERIAL_NUM, SCRIPT_ROOT]

    movup.5 movup.5 dup movdn.2
    # => [storage_ptr, storage_len, storage_ptr, STORAGE_COMMITMENT, SERIAL_NUM, SCRIPT_ROOT]

    add swap
    # => [storage_ptr, end_ptr, STORAGE_COMMITMENT, SERIAL_NUM, SCRIPT_ROOT]

    movdn.5 movdn.5
    # => [STORAGE_COMMITMENT, storage_ptr, end_ptr, SERIAL_NUM, SCRIPT_ROOT]

    adv.insert_mem
    # => [STORAGE_COMMITMENT, storage_ptr, end_ptr, SERIAL_NUM, SCRIPT_ROOT]

    movup.4 drop movup.4 drop
    # => [STORAGE_COMMITMENT, SERIAL_NUM, SCRIPT_ROOT]

    movdnw.2
    # => [SERIAL_NUM, SCRIPT_ROOT, STORAGE_COMMITMENT]

    padw adv.insert_hdword hmerge
    # => [SERIAL_HASH, SCRIPT_ROOT, STORAGE_COMMITMENT]

    swapw adv.insert_hdword hmerge
    # => [SERIAL_SCRIPT_HASH, STORAGE_COMMITMENT]

    swapw adv.insert_hdword hmerge
    # => [RECIPIENT]
end

#! Returns the RECIPIENT for a specified SERIAL_NUM, SCRIPT_ROOT, and storage commitment.
#!
#! Inputs:  [SERIAL_NUM, SCRIPT_ROOT, STORAGE_COMMITMENT]
#! Outputs: [RECIPIENT]
#!
#! Where:
#! - SERIAL_NUM is the serial number of the recipient.
#! - SCRIPT_ROOT is the commitment of the note script.
#! - STORAGE_COMMITMENT is the commitment of the note storage.
#! - RECIPIENT is the recipient of the note.
#!
#! Invocation: exec
export.build_recipient_hash
    padw hmerge
    # => [SERIAL_NUM_HASH, SCRIPT_ROOT, STORAGE_COMMITMENT]

    swapw hmerge
    # => [MERGE_SCRIPT, STORAGE_COMMITMENT]

    swapw hmerge
    # [RECIPIENT]
end

#! Extracts the sender ID from the provided metadata word.
#!
#! Inputs:  [METADATA]
#! Outputs: [sender_id_prefix, sender_id_suffix]
#!
#! Where:
#! - METADATA is the metadata of some note.
#! - sender_{prefix,suffix} are the prefix and suffix felts of the sender ID of the note which
#!   metadata was provided.
export.extract_sender_from_metadata
    # => [aux, merged_tag_hint_payload, merged_sender_id_type_hint_tag, sender_id_prefix]

    # drop aux felt and the felt containing tag, execution hint and payload
    drop drop
    # => [merged_sender_id_type_hint_tag, sender_id_prefix]

    # extract suffix of sender from merged layout, which means clearing the least significant byte
    exec.account_id::shape_suffix
    # => [sender_id_suffix, sender_id_prefix]

    # rearrange suffix and prefix
    swap
    # => [sender_id_prefix, sender_id_suffix]
end
