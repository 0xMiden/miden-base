use.std::mem

use.miden::kernel_proc_offsets
use.miden::note
use.miden::contracts::wallets::basic->wallet

#Â ERRORS
# =================================================================================================

const.ERR_NOTE_DATA_DOES_NOT_MATCH_COMMITMENT="note data does not match the commitment"

const.ERR_NOTE_INVALID_STORAGE_LENGTH="the specified note storage length does not match the actual number"

# ACTIVE NOTE PROCEDURES
# =================================================================================================
#
# By the "active note" notion here we assume the note which is currently being processed by the
# transaction kernel.

#! Writes the assets of the active note into memory starting at the specified address.
#!
#! Inputs:  [dest_ptr]
#! Outputs: [num_assets, dest_ptr]
#!
#! Where:
#! - dest_ptr is the memory address to write the assets.
#! - num_assets is the number of assets in the active note.
#!
#! Panics if:
#! - no note is currently active.
#!
#! Invocation: exec
export.get_assets
    # pad the stack
    padw padw padw push.0.0
    # => [pad(14), dest_ptr]

    # push the flag indicating that we want to request assets info from the active note
    push.1
    # => [is_active_note = 1, pad(14), dest_ptr]

    exec.kernel_proc_offsets::input_note_get_assets_info_offset
    # => [offset, is_active_note = 1, pad(14), dest_ptr]

    syscall.exec_kernel_proc
    # => [ASSETS_COMMITMENT, num_assets, pad(11), dest_ptr]

    # clean the stack
    swapdw dropw dropw movup.7 movup.7 movup.7 drop drop drop
    # => [ASSETS_COMMITMENT, num_assets, dest_ptr]

    # write the assets from the advice map into memory
    exec.note::write_assets_to_memory
    # => [num_assets, dest_ptr]
end

#! Returns the recipient of the active note.
#!
#! Inputs:  []
#! Outputs: [RECIPIENT]
#!
#! Where:
#! - RECIPIENT is the commitment to the active note's script, storage, the serial number.
#!
#! Panics if:
#! - no note is currently active.
#!
#! Invocation: exec
export.get_recipient
    # pad the stack
    padw padw padw push.0.0
    # => [pad(14)]

    # push the flag indicating that we want to request recipient from the active note
    push.1
    # => [is_active_note = 1, pad(14)]

    exec.kernel_proc_offsets::input_note_get_recipient_offset
    # => [offset, is_active_note = 1, pad(14)]

    syscall.exec_kernel_proc
    # => [RECIPIENT, pad(12)]

    # clean the stack
    swapdw dropw dropw swapw dropw
    # => [RECIPIENT]
end

#! Writes the active note's storage items to memory starting at the specified address.
#!
#! Inputs:
#!   Stack: [dest_ptr]
#!   Advice Map: { NOTE_STORAGE_COMMITMENT: [STORAGE_ITEMS] }
#! Outputs:
#!   Stack: [storage_len, dest_ptr]
#!
#! Where:
#! - dest_ptr is the memory address to write the note storage.
#! - NOTE_STORAGE_COMMITMENT is the sequential hash of the padded note's storage items.
#! - STORAGE_ITEMS is the data corresponding to the note's storage items.
#!
#! Panics if:
#! - no note is currently active.
#!
#! Invocation: exec
export.get_storage
    # pad the stack
    padw padw padw push.0.0
    # => [pad(14), dest_ptr]

    # push the flag indicating that we want to request inputs info from the active note
    push.1
    # => [is_active_note = 1, pad(14), dest_ptr]

    exec.kernel_proc_offsets::input_note_get_storage_info_offset
    # => [offset, is_active_note = 1, pad(14), dest_ptr]

    syscall.exec_kernel_proc
    # => [NOTE_STORAGE_COMMITMENT, storage_len, pad(11), dest_ptr]

    # clean the stack
    swapdw dropw dropw
    movup.5 drop movup.5 drop movup.5 drop
    # => [NOTE_STORAGE_COMMITMENT, storage_len, dest_ptr]

    # write the storage to the memory using the provided destination pointer
    exec.write_storage_to_memory
    # => [storage_len, dest_ptr]
end

#! Returns the metadata of the active note.
#!
#! Inputs:  []
#! Outputs: [METADATA]
#!
#! Where:
#! - METADATA is the metadata of the active note.
#!
#! Panics if:
#! - no note is currently active.
#!
#! Invocation: exec
export.get_metadata
    # pad the stack
    padw padw padw push.0.0
    # => [pad(14)]

    # push the flag indicating that we want to request metadata from the active note
    push.1
    # => [is_active_note = 1, pad(14)]

    exec.kernel_proc_offsets::input_note_get_metadata_offset
    # => [offset, is_active_note = 1, pad(14)]

    syscall.exec_kernel_proc
    # => [METADATA, pad(12)]

    # clean the stack
    swapdw dropw dropw swapw dropw
    # => [METADATA]
end

#! Returns the sender of the active note.
#!
#! Inputs:  []
#! Outputs: [sender_id_prefix, sender_id_suffix]
#!
#! Where:
#! - sender_{prefix,suffix} are the prefix and suffix felts of the sender of the active note.
#!
#! Panics if:
#! - no note is currently active.
#!
#! Invocation: exec
export.get_sender
    # pad the stack
    padw padw padw push.0.0
    # => [pad(14)]

    # push the flag indicating that we want to request metadata from the active note
    push.1
    # => [is_active_note = 1, pad(14)]

    exec.kernel_proc_offsets::input_note_get_metadata_offset
    # => [offset, is_active_note = 1, pad(14)]

    syscall.exec_kernel_proc
    # => [METADATA, pad(12)]

    # extract the sender ID from the metadata word
    exec.note::extract_sender_from_metadata
    # => [sender_id_prefix, sender_id_suffix, pad(12)]

    # clean the stack
    swapw dropw swapw dropw movdn.5 movdn.5 dropw
    # => [sender_id_prefix, sender_id_suffix]
end

#! Returns the serial number of the active note.
#!
#! Inputs:  []
#! Outputs: [SERIAL_NUMBER]
#!
#! Where:
#! - SERIAL_NUMBER is the serial number of the active note.
#!
#! Panics if:
#! - no note is currently active.
#!
#! Invocation: exec
export.get_serial_number
    # pad the stack
    padw padw padw push.0.0
    # => [pad(14)]

    # push the flag indicating that we want to request serial number from the active note
    push.1
    # => [is_active_note = 1, pad(14)]

    exec.kernel_proc_offsets::input_note_get_serial_number_offset
    # => [offset, is_active_note = 1, pad(14)]

    syscall.exec_kernel_proc
    # => [SERIAL_NUMBER, pad(12)]

    # clean the stack
    swapdw dropw dropw swapw dropw
    # => [SERIAL_NUMBER]
end

#! Returns the script root of the active note.
#!
#! Inputs:  []
#! Outputs: [SCRIPT_ROOT]
#!
#! Where:
#! - SCRIPT_ROOT is the script root of the active note.
#!
#! Panics if:
#! - no note is currently active.
#!
#! Invocation: exec
export.get_script_root
    # pad the stack
    padw padw padw push.0.0
    # => [pad(14)]

    # push the flag indicating that we want to request script root from the active note
    push.1
    # => [is_active_note = 1, pad(14)]

    exec.kernel_proc_offsets::input_note_get_script_root_offset
    # => [offset, is_active_note = 1, pad(14)]

    syscall.exec_kernel_proc
    # => [SCRIPT_ROOT, pad(12)]

    # clean the stack
    swapdw dropw dropw swapw dropw
    # => [SCRIPT_ROOT]
end

#! Adds all assets from the active note to the native account's vault.
#!
#! Inputs:  []
#! Outputs: []
export.add_assets_to_account.1024
    # write assets to local memory starting at offset 0
    # we have allocated 4 * MAX_ASSETS_PER_NOTE number of locals so all assets should fit
    # since the asset memory will be overwritten, we don't have to initialize the locals to zero
    locaddr.0 exec.get_assets
    # => [num_of_assets, ptr = 0]

    # compute the pointer at which we should stop iterating
    mul.4 dup.1 add
    # => [end_ptr, ptr]

    # pad the stack and move the pointer to the top
    padw movup.5
    # => [ptr, EMPTY_WORD, end_ptr]

    # loop if the amount of assets is non-zero
    dup dup.6 neq
    # => [should_loop, ptr, EMPTY_WORD, end_ptr]

    while.true
        # => [ptr, EMPTY_WORD, end_ptr]

        # save the pointer so that we can use it later
        dup movdn.5
        # => [ptr, EMPTY_WORD, ptr, end_ptr]

        # load the asset
        mem_loadw_be
        # => [ASSET, ptr, end_ptr]

        # pad the stack before call
        padw swapw padw padw swapdw
        # => [ASSET, pad(12), ptr, end_ptr]

        # add asset to the account
        call.wallet::receive_asset
        # => [pad(16), ptr, end_ptr]

        # clean the stack after call
        dropw dropw dropw
        # => [EMPTY_WORD, ptr, end_ptr]

        # increment the pointer and continue looping if ptr != end_ptr
        movup.4 add.4 dup dup.6 neq
        # => [should_loop, ptr+4, EMPTY_WORD, end_ptr]
    end
    # => [ptr', EMPTY_WORD, end_ptr]

    # clear the stack
    drop dropw drop
    # => []
end

#Â HELPER PROCEDURES
# =================================================================================================

#! Writes the note storage stored in the advice map to the memory specified by the provided
#! destination pointer.
#!
#! Inputs:
#!   Operand stack: [NOTE_STORAGE_COMMITMENT, storage_len, dest_ptr]
#!   Advice map: {
#!     NOTE_STORAGE_COMMITMENT: [[STORAGE_ITEMS]]
#!   }
#! Outputs:
#!   Operand stack: [storage_len, dest_ptr]
proc.write_storage_to_memory
    # load the storage items from the advice map to the advice stack
    adv.push_mapvaln
    # OS => [NOTE_STORAGE_COMMITMENT, storage_len, dest_ptr]
    # AS => [advice_storage_len, [STORAGE_ITEMS]]

    # move the storage length obtained from advice map to the operand stack
    adv_push.1 dup.5
    # OS => [storage_len, advice_storage_len, NOTE_STORAGE_COMMITMENT, storage_len, dest_ptr]
    # AS => [[STORAGE_ITEMS]]

    # Validate the note storage length. Round up the storage length to the next multiple of 8: that
    # value should be equal to the length obtained from the `adv.push_mapvaln` procedure.
    u32divmod.8 neq.0 add mul.8
    # OS => [rounded_up_storage_len, advice_storage_len, NOTE_STORAGE_COMMITMENT, storage_len, dest_ptr]
    # AS => [[STORAGE_ITEMS]]

    assert_eq.err=ERR_NOTE_INVALID_STORAGE_LENGTH
    # OS => [NOTE_STORAGE_COMMITMENT, storage_len, dest_ptr]
    # AS => [[STORAGE_ITEMS]]

    # calculate the number of words required to store the inputs
    dup.4 u32divmod.4 neq.0 add
    # OS => [num_words, NOTE_STORAGE_COMMITMENT, storage_len, dest_ptr]
    # AS => [[STORAGE_ITEMS]]

    # round up the number of words to the next multiple of 2
    dup is_odd add
    # OS => [even_num_words, NOTE_STORAGE_COMMITMENT, storage_len, dest_ptr]
    # AS => [[STORAGE_ITEMS]]

    # prepare the stack for the `pipe_preimage_to_memory` procedure
    dup.6 swap
    # OS => [even_num_words, dest_ptr, NOTE_STORAGE_COMMITMENT, storage_len, dest_ptr]
    # AS => [[STORAGE_ITEMS]]

    # write the inputs from the advice stack into memory
    exec.mem::pipe_preimage_to_memory drop
    # OS => [storage_len, dest_ptr]
    # AS => []
end
