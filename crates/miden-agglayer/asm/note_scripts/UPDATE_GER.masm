use miden::agglayer::bridge_in
use miden::protocol::active_note
use miden::protocol::active_account
use miden::protocol::account_id
use miden::protocol::note
use miden::standards::attachments::network_account_target

# CONSTANTS
# =================================================================================================
const UPDATE_GER_NOTE_NUM_STORAGE_ITEMS = 8
const STORAGE_PTR_GER_LOWER = 0
const STORAGE_PTR_GER_UPPER = 4

# ERRORS
# =================================================================================================
const ERR_UPDATE_GER_UNEXPECTED_NUMBER_OF_STORAGE_ITEMS = "UPDATE_GER script expects exactly 8 note storage items"
const ERR_UPDATE_GER_TARGET_ACCOUNT_MISMATCH = "UPDATE_GER note attachment target account does not match consuming account"

# NOTE SCRIPT
# =================================================================================================

#! Agglayer Bridge UPDATE_GER script: updates the GER by calling the bridge_in::update_ger function.
#!
#! This note can only be consumed by the specific agglayer bridge account whose ID is provided
#! in the note attachment (target_account_id).
#!
#! Requires that the account exposes:
#! - agglayer::bridge_in::update_ger procedure.
#!
#! Inputs:  [ARGS, pad(12)]
#! Outputs: [pad(16)]
#! NoteStorage layout (8 felts total):
#! - GER_LOWER [0..3]
#! - GER_UPPER [4..7]
#!
#! Panics if:
#! - account does not expose update_ger procedure.
#! - target account ID does not match the consuming account ID.
#! - number of note storage items is not exactly 8.
begin
    dropw
    # => [pad(16)]

    # Ensure note attachment targets the consuming bridge account.
    exec.active_note::get_metadata
    # => [NOTE_ATTACHMENT, METADATA_HEADER, pad(8)]

    swapw
    # => [METADATA_HEADER, NOTE_ATTACHMENT, pad(8)]

    exec.note::extract_attachment_info_from_metadata
    # => [attachment_kind, attachment_scheme, NOTE_ATTACHMENT, pad(8)]

    swap
    # => [attachment_scheme, attachment_kind, NOTE_ATTACHMENT, pad(8)]

    exec.network_account_target::get_id
    # => [target_id_prefix, target_id_suffix, pad(14)]

    exec.active_account::get_id
    # => [account_id_prefix, account_id_suffix, target_id_prefix, target_id_suffix, pad(14)]

    exec.account_id::is_equal
    assert.err=ERR_UPDATE_GER_TARGET_ACCOUNT_MISMATCH
    # => [pad(16)]

    # proceed with the GER update logic

    push.STORAGE_PTR_GER_LOWER exec.active_note::get_storage
    # => [num_storage_items, dest_ptr, pad(16)]

    push.UPDATE_GER_NOTE_NUM_STORAGE_ITEMS assert_eq.err=ERR_UPDATE_GER_UNEXPECTED_NUMBER_OF_STORAGE_ITEMS drop
    # => [pad(16)]

    # Load GER_LOWER and GER_UPPER from note storage
    mem_loadw_be.STORAGE_PTR_GER_UPPER
    swapw mem_loadw_be.STORAGE_PTR_GER_LOWER
    # => [GER_LOWER[4], GER_UPPER[4]]

    call.bridge_in::update_ger
    # => []

end