use miden::agglayer::agglayer_faucet -> agg_faucet
use miden::protocol::account_id
use miden::protocol::active_account
use miden::protocol::active_note
use miden::protocol::note
use miden::core::crypto::hashes::keccak256
use miden::core::crypto::hashes::rpo256
# CONSTANTS
# =================================================================================================

const CLAIM_NOTE_NUM_INPUTS = 620  # Updated to match new Solidity claimAsset inputs
const CLAIM_PROOF_DATA_SIZE = 608  # Claim Data + SMT proof is 604 felts
const CLAIM_PROOF_DATA_WORDS = 570  # 570 felts directly

const AGG_FAUCET_PREFIX = 11
const AGG_FAUCET_SUFFIX = 10

# ERRORS
# =================================================================================================

const ERR_CLAIM_TARGET_ACCT_MISMATCH = "CLAIM's target account address and transaction address do not match"
const ERR_CLAIM_WRONG_NUMBER_OF_INPUTS = "CLAIM note must have exactly 614 inputs"

# Inputs: []
# Output: []
proc assert_aggfaucet_is_consumer
    # Load agglayer faucet ID (assumes active_note::get_inputs has been called)
    mem_load.AGG_FAUCET_SUFFIX mem_load.AGG_FAUCET_PREFIX
    # => [agg_faucet_prefix, agg_faucet_suffix]

    exec.active_account::get_id
    # => [account_id_prefix, account_id_suffix, agg_faucet_prefix, agg_faucet_suffix]

    # ensure only the specified agglayer faucet can consume this CLAIM note, not any other account
    exec.account_id::is_equal assert.err=ERR_CLAIM_TARGET_ACCT_MISMATCH
    # => []
end

#! Agglayer Faucet CLAIM script: claims assets by calling the agglayer faucet's claim function.
#! This note is intended to be consumed by an agglayer faucet account and will create a P2ID note on consumption.
#!
#! Requires that the account exposes:
#! - agglayer::agglayer_faucet::claim procedure.
#!
#! Inputs:  [ARGS, pad(16)]
#! Outputs: [pad(16)]
#!
#! Current note inputs are assumed to be as follows:
#! - execution_hint: Execution hint for the output note (always 0)
#! - aux: Auxiliary data for the output note
#! - tag: Note tag for the output note
#! - amount: The amount to claim
#! - SERIAL_NUM: Serial number of the output note (4 elements)
#! - target_account_id: P2ID recipient account ID (suffix, prefix)
#! - agg_faucet_id: Agglayer faucet account ID (suffix, prefix)
#! - smtProof: SMT proof data (570 felts) matching Solidity claimAsset parameter
#! - index: Index for the claim (u32 as Felt)
#! - mainnetExitRoot: Mainnet exit root hash (8 felts)
#! - rollupExitRoot: Rollup exit root hash (8 felts)
#! - originNetwork: Origin network identifier (1 felt)
#! - originTokenAddress: Origin token address (5 felts)
#! - destinationNetwork: Destination network identifier (1 felt)
#! - destinationAddress: Destination address (5 felts)
#! - metadata: Additional metadata (variable length)
#!
#! Panics if:
#! - account does not expose claim procedure.
#! - the number of inputs is not exactly 612.
#! - agglayer faucet account ID does not match the consuming account ID.
begin
    dropw
    # => []

    # load CLAIM note inputs
    push.0 exec.active_note::get_inputs
    # => [total_inputs, inputs_ptr]

    # Verify we have exactly 586 inputs
    push.CLAIM_NOTE_NUM_INPUTS eq assert.err=ERR_CLAIM_WRONG_NUMBER_OF_INPUTS drop
    # => []

    # check consuming account == aggfaucet
    exec.assert_aggfaucet_is_consumer
    # => []

    # Compute the RPO_COMMITMENT of the claim proof data
    # Hash the proof data data (600 Felts starting at offset 12)
    # The proof data starts at memory address 12
    push.608 push.12
    # => [start_ptr, end_ptr]

    exec.rpo256::hash_elements
    # => [CLAIM_PROOF_RPO_COMMITMENT]

    # Prepare for insertion into AdviceMap
    push.CLAIM_PROOF_DATA_SIZE add.12 push.12
    # => [start_ptr, end_ptr, CLAIM_PROOF_RPO_COMMITMENT]

    movdn.5 movdn.5
    # => [CLAIM_PROOF_RPO_COMMITMENT, start_ptr, end_ptr]

    # Insert Proof Data into AdviceMap
    adv.insert_mem
    # OS => [CLAIM_PROOF_RPO_COMMITMENT, start_ptr, end_ptr]
    # AS => {CLAIM_PROOF_RPO_COMMITMENT: mem[start_ptr..end_ptr] }

    # Pass the proof data commitment to the faucet's claim procedure
    call.agg_faucet::claim
    # => []

    dropw dropw dropw dropw
    # => []
end

