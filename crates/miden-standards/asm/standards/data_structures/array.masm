# The MASM code for the Array abstraction.
#
# It provides an abstraction layer over a storage map, treating it as an array,
# with "set" and "get" for storing and retrieving words by (slot_id, index).
# The array can store up to 2^64 - 2^32 + 1 elements (indices 0 to 2^64 - 2^32).
#
# Using this Array utility requires that the underlying storage map is already created and 
# initialized as part of an account component, under the given slot ID.

use miden::protocol::active_account
use miden::protocol::native_account

type BeWord = struct @bigendian { a: felt, b: felt, c: felt, d: felt }

# PROCEDURES
# =================================================================================================

#! Sets a word in the array at the specified index.
#!
#! Inputs:  [slot_id_prefix, slot_id_suffix, index, VALUE, pad(9)]
#! Outputs: [OLD_VALUE, pad(12)]
#!
#! Where:
#! - slot_id_{prefix, suffix} are the prefix and suffix felts of the slot identifier.
#! - index is the index at which to store the value (0 to 2^64 - 2^32).
#! - VALUE is the word to store at the specified index.
#!
#! Invocation: call
pub proc set(slot_id_prefix: felt, slot_id_suffix: felt, index: felt, value: BeWord) -> BeWord
    # Build KEY = [index, 0, 0, 0]
    push.0.0.0 movup.5
    # => [index, 0, 0, 0, slot_id_prefix, slot_id_suffix, VALUE, pad(9)]
    #
    # Keep stack depth at 16 before the exec below.
    movup.10 drop
    movup.10 drop
    movup.10 drop
    # => [index, 0, 0, 0, slot_id_prefix, slot_id_suffix, VALUE, pad(6)]

    # Move slot IDs above the key
    movup.5 movup.5
    # => [slot_id_prefix, slot_id_suffix, KEY, VALUE, pad(6)]

    exec.native_account::set_map_item
    # => [OLD_VALUE, pad(12)]
end

#! Gets a word from the array at the specified index.
#!
#! Inputs:  [slot_id_prefix, slot_id_suffix, index, pad(13)]
#! Outputs: [VALUE, pad(12)]
#!
#! Where:
#! - slot_id_{prefix, suffix} are the prefix and suffix felts of the slot identifier.
#! - index is the index of the element to retrieve (0 to 2^64 - 2^32).
#! - VALUE is the word stored at the specified index (zero if not set).
#!
#! Invocation: call
pub proc get(slot_id_prefix: felt, slot_id_suffix: felt, index: felt) -> BeWord
    # Build KEY = [index, 0, 0, 0]
    push.0.0.0 movup.5
    # => [index, 0, 0, 0, slot_id_prefix, slot_id_suffix, pad(13)]
    #
    # Keep stack depth at 16 before the exec below.
    movup.6 drop
    movup.6 drop
    movup.6 drop
    # => [index, 0, 0, 0, slot_id_prefix, slot_id_suffix, pad(10)]

    # Move slot IDs above the key
    movup.5 movup.5
    # => [slot_id_prefix, slot_id_suffix, KEY, pad(10)]

    exec.active_account::get_map_item
    # => [VALUE, pad(15)]
    
    # truncate the stack
    repeat.3 movup.4 drop end
    # => [VALUE, pad(12)]
end
