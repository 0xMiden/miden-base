# miden::standards::attachment::network_account_target
#
# Provides a standardized way to work with network account targets.

use miden::protocol::active_note

# CONSTANTS
# ================================================================================================

#! The attachment scheme for NetworkAccountTarget attachments.
#! This is a valid u32 that can be compared against an extracted attachment scheme.
pub const NETWORK_ACCOUNT_TARGET_ATTACHMENT_SCHEME = 1

# ERRORS
# ================================================================================================
const ERR_ATTACHMENT_SCHEME_MISMATCH = "expected network account target attachment scheme"

#! Returns the ID encoded in the attachment.
#!
#! The attachment is expected to have the following layout:
#! [0, exec_hint_tag, account_id_prefix, account_id_suffix]
#!
#! Inputs:  [NOTE_ATTACHMENT, NOTE_METADATA_HEADER]
#! Outputs: [account_id_prefix, account_id_suffix]
#!
#! Where:
#! - account_id_{prefix,suffix} are the prefix and suffix felts of an account ID.
#!
#! Invocation: exec
pub proc get_id
    swapw
    # verify that the attachment scheme is correct
    # => [attachment_kind_scheme, METADATA_HEADER[1..4], NOTE_ATTACHMENT]

    # deconstruct the attachment_kind_scheme to extract the attachment_scheme
    # attachment_kind_scheme = [30 zero bits | attachment_kind (2 bits) | attachment_scheme (32 bits)]
    # u32split splits into [high, low] where low is attachment_scheme
    u32split drop
    # => [attachment_scheme, METADATA_HEADER[1..4], NOTE_ATTACHMENT]

    eq.NETWORK_ACCOUNT_TARGET_ATTACHMENT_SCHEME assert.err=ERR_ATTACHMENT_SCHEME_MISMATCH
    drop drop drop
    # => [NOTE_ATTACHMENT]

    drop drop
    # => [account_id_prefix, account_id_suffix]
end

#! Creates a new attachment of type NetworkAccountTarget with the following layout:
#! [0, exec_hint_tag, account_id_prefix, account_id_suffix]
#!
#! Inputs:  [account_id_prefix, account_id_suffix, exec_hint]
#! Outputs: [NOTE_ATTACHMENT]
#!
#! Where:
#! - account_id_{prefix,suffix} are the prefix and suffix felts of an account ID.
#! - exec_hint is the execution hint for the note.
#!
#! Invocation: exec
pub proc new_attachment
    movup.2
    push.0
end
