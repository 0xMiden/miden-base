# miden::standards::utils::access::ownable
#
# Provides ownership management functionality for account components.
# This template can be imported and used by any component that needs owner controls.
#
# NOTE: This file is kept as reference/documentation only. The ownership logic is implemented
# directly in the modules that use it (e.g., network_fungible.masm) because template modules
# cannot access storage slots registered by the component that imports them.

use miden::protocol::active_account
use miden::protocol::account_id
use miden::protocol::active_note
use miden::protocol::native_account

# CONSTANTS
# ================================================================================================

# The slot in this component's storage layout where the owner config is stored.
const OWNER_CONFIG_SLOT=word("miden::standards::utils::access::ownable::owner_config")

# ERRORS
# ================================================================================================

const ERR_ONLY_OWNER="note sender is not the owner"

# INTERNAL PROCEDURES
# ================================================================================================

#! Returns the owner AccountId from storage.
#!
#! Inputs:  []
#! Outputs: [owner_prefix, owner_suffix, 0, 0]
#!
#! Where:
#! - owner_{prefix, suffix} are the prefix and suffix felts of the owner AccountId.
proc owner
    push.OWNER_CONFIG_SLOT[0..2] exec.active_account::get_item
    # => [owner_prefix, owner_suffix, 0, 0]
end

#! Checks if the note sender is the owner of this component.
#!
#! Inputs:  []
#! Outputs: [is_owner]
#!
#! Where:
#! - is_owner is 1 if the sender is the owner, 0 otherwise.
proc is_owner
    exec.owner
    # => [owner_prefix, owner_suffix, 0, 0]

    exec.active_note::get_sender
    # => [sender_prefix, sender_suffix, owner_prefix, owner_suffix, 0, 0]

    exec.account_id::is_equal
    # => [are_equal, 0, 0]

    movdn.2 drop drop
    # => [is_owner]
end

#! Checks if the note sender is the owner and panics if not.
#!
#! Inputs:  []
#! Outputs: []
#!
#! Panics if:
#! - the note sender is not the owner.
pub proc only_owner
    exec.is_owner
    # => [is_owner]
    assert.err=ERR_ONLY_OWNER
    # => []
end

# PUBLIC INTERFACE
# ================================================================================================

#! Returns the owner AccountId.
#!
#! Inputs:  []
#! Outputs: [owner_prefix, owner_suffix, pad(14)]
#!
#! Where:
#! - owner_{prefix, suffix} are the prefix and suffix felts of the owner AccountId.
#!
#! Invocation: call
pub proc get_owner
    exec.owner
    # => [owner_prefix, owner_suffix, 0, 0]
    
    # ------ Clean up and return pad(14) ------
    drop drop
    # => [owner_prefix, owner_suffix, pad(14)]
end

#! Transfers ownership to a new account.
#!
#! Can only be called by the current owner.
#!
#! Inputs:  [new_owner_prefix, new_owner_suffix, pad(14)]
#! Outputs: [pad(16)]
#!
#! Where:
#! - new_owner_{prefix, suffix} are the prefix and suffix felts of the new owner AccountId.
#!
#! Panics if:
#! - the note sender is not the owner.
#!
#! Invocation: call
pub proc transfer_ownership
    # Check that the caller is the owner
    exec.only_owner
    # => [new_owner_prefix, new_owner_suffix, pad(14)]

    # get_item returns [owner_prefix, owner_suffix, 0, 0]
    # so the Word in storage is [0, 0, owner_suffix, owner_prefix] (top to bottom)
    # To write, we need: [new_owner_prefix, new_owner_suffix, 0, 0]
    # The zeros from pad(14) already serve as word[1] and word[0]!
    # Stack: [new_owner_prefix, new_owner_suffix, 0, 0, pad(12)]
    push.OWNER_CONFIG_SLOT[0..2]
    # => [slot_prefix, slot_suffix, new_owner_prefix, new_owner_suffix, 0, 0, pad(12)]

    exec.native_account::set_item
    # => [OLD_OWNER_WORD, pad(12)]

    # Remove OLD_OWNER_WORD (4 felts) and keep pad(12)
    dropw
    # => [pad(16)]
end

